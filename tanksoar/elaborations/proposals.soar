## Move Operator Proposal
# If the task is tanksoar and the tank is not blocked in the forward
# direction, propose the move operator.
sp {propose*move
   (state <s> ^name tanksoar
              ^io.input-link.blocked.forward no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name move
        ^actions.move.direction forward)}
sp {wander*propose*move
   (state <s> ^name wander
              ^io.input-link.blocked.forward no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name move
        ^actions.move.direction forward)}

## Turn Operator Proposal
# If the tank is blocked in the forward direction, and not blocked in the
# right or left directions, then propose the turn operator for the unblocked
# direction. Also create an indifferent preference for the operator.
sp {wander*propose*turn
   (state <s> ^name wander
              ^io.input-link.blocked <b>)
   (<b> ^forward yes
        ^ { << left right >> <dir> } no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions <a>)
   (<a> ^rotate.direction <dir>
        ^radar.switch on
        ^radar-power.setting 13)}
sp {propose*turn
   (state <s> ^name tanksoar
              ^io.input-link.blocked <b>)
   (<b> ^forward yes
        ^ { << left right >> <direction> } no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions <a>)
   (<a> ^rotate.direction <direction>
        ^radar.switch on
        ^radar-power.setting 13)}
# This operator will also turn on the radar and set the radar-power to 13.
# If the tank is blocked in the forward direction, and in both the right or
# left directions, then propose the turn operator left.
sp {propose*turn*backward
   (state <s> ^name tanksoar
              ^io.input-link.blocked <b>)
   (<b> ^forward yes ^left yes ^right yes)
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions.rotate.direction left)}
sp {wander*propose*turn*backward
   (state <s> ^name wander
              ^io.input-link.blocked <b>)
   (<b> ^forward yes
        ^left yes
        ^right yes)
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions.rotate.direction left)}
## Radar-off Operator Proposal
# If the radar is on but no energy, health, missiles and tanks visible,
# then propose the radar-off operator
## Radar-off Search Control
# If radar-off is proposed, then prefer it to move and turn.
sp {propose*radar-off
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> ^radar-status on
        -^radar.<< energy health missiles tank >>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name radar-off
        ^actions.radar.switch off)}
sp {select*radar-off*move
   (state <s> ^name tanksoar
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name radar-off)
   (<o2> ^name << turn move >>)
-->
   (<s> ^operator <o1> > <o2>)}

##Wander Operator Proposal
# If there is no tank detected on radar, and the sound is silent, and there
# is no incoming, then propose the wander operator.
sp {propose*wander
   (state <s> ^name tanksoar
              ^io.input-link <io>)
   (<io> ^sound silent
        -^radar.tank
        -^incoming.<dir> yes)
-->
   (<s> ^operator <o> + =)
   (<o> ^name wander)}

sp {propose*chase
   (state <s> ^name tanksoar
              ^io.input-link <io>
             -^missiles-energy low)
   (<io> ^sound <> silent
        -^radar.tank)
-->
   (<s> ^operator <o> + =)
   (<o> ^name chase)}

## Propose Move Operator
## If the state is named chase and the sound is coming from the forward position, propose move forward.
sp {chase*propose*move
   (state <s> ^name chase
              ^sound-direction forward
              ^io.input-link.blocked.forward no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name move
        ^actions.move.direction forward)}

## Propose Turn Operator
## If the state is named chase and the sound is coming from left or right, turn that direction.
sp {chase*propose*turn
   (state <s> ^name chase
              ^sound-direction {<< left right >> <direction>})
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions.rotate.direction <direction>)}

## Propose Turn Operator Backward
## If the state is named chase and the sound is coming from backward, turn left.
sp {chase*propose*backward
   (state <s> ^name chase
              ^sound-direction backward)
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions.rotate.direction left)}

##Propose Attack Operator
# If the state is tanksoar, and there is a tank on radar, and health and energy are not low, then
# propose the attack operator.
sp {propose*attack
   (state <s> ^name tanksoar
              ^io.input-link.radar.tank -^missilesenergy low)
-->
   (<s> ^operator <o> + =)
   (<o> ^name attack)}

##Propose Fire-missile Operator
# If the state is attack and there is a tank on radar in the center, then propose the fire missile operator.
sp {attack*propose*fire-missile
   (state <s> ^name attack
              ^io.input-link <il>)
   (<il> ^radar.tank.position center
         ^missiles > 0)
-->
   (<s> ^operator <o> + >)
   (<o> ^name fire-missile
        ^actions.fire.weapon missile)}

##Propose Slide Operator
# If the state is attack and there is a tank on radar that is not in the center, and there is not a tank in the #
#center, and there is an open spot in the direction of the tank, then propose the slide operator in the
#direction of the tank.
sp {attack*propose*slide
   (state <s> ^name attack
              ^io.input-link <input>)
   (<input> ^blocked.<dir> no
            ^radar <r>)
   (<r> ^tank.position { << left right >> <dir> }
       -^tank.position center)
-->
   (<s> ^operator <o> + =)
   (<o> ^name slide
        ^actions.move.direction <dir>)}

##Propose Move-Forward Operator
# If the state is attack and there is a tank on radar that is not in the center, and there is not a tank in the #
# center, and the tank is blocked in that direction then propose move-forward.
sp {attack*propose*move-forward
   (state <s> ^name attack
              ^io.input-link <input>)
   (<input> ^blocked.<dir> yes
            ^radar <r>)
   (<r> ^tank <t>
       -^tank.position center)
   (<t> ^position { << left right >> <dir> }
        ^distance <> 0)
-->
   (<s> ^operator <o> + =)
   (<o> ^name move-forward
        ^actions.move.direction forward)}

###Propose Turn Operator
## If the state is attack and there is a tank on radar that right next to the tank, then propose turning in that
## direction and firing.
sp {attack*propose*turn
   (state <s> ^name attack
              ^io.input-link.radar.tank <tank>)
   (<tank> ^distance 0
           ^position { << left right >> <dir> })
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions <a>)
   (<a> ^rotate.direction <dir>
        ^fire.weapon missile)}

##Propose Retreat Operator
# If the state is tanksoar and the sound sensor is not silent or there is a tank on radar or there is an #
# incoming missile, and health is low or the energy is low, then propose the retreat operator.
sp {propose*retreat*sound
   (state <s> ^name tanksoar
              ^missiles-energy low
              ^io.input-link.sound {<direction> <> silent})
-->
   (<s> ^operator <o> + =)
   (<o> ^name retreat)}
sp {propose*retreat*radar
   (state <s> ^name tanksoar
              ^missiles-energy low
              ^io.input-link.radar.tank)
-->
   (<s> ^operator <o> + =)
   (<o> ^name retreat)}
sp {propose*retreat*incoming
   (state <s> ^name tanksoar
              ^missiles-energy low
              ^io.input-link.incoming.<dir> yes)
-->
   (<s> ^operator <o> + =)
   (<o> ^name retreat)}

##Propose Retreat Operator
# If the state is tanksoar and the tank is under attack but cannot not directly sense the other tank, then #
# propose the retreat operator.
sp {propose*retreat*incoming*not-sensed
   (state <s> ^name tanksoar
              ^io.input-link <io>)
   (<io> ^incoming.<dir> yes
        -^radar.tank
         ^sound silent)
-->
   (<s> ^operator <o> + =)
   (<o> ^name retreat)}

sp {retreat*propose*move
   (state <s> ^name retreat
              ^direction <dir>
              ^superstate.side-direction.<dir> <ndir>
             -^direction <ndir>
             -^avoid-direction <ndir>
              ^io.input-link.blocked.<ndir> no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name move
        ^actions.move.direction <ndir>)}

#### Propose wait for a state-no-change
sp {top-state*propose*wait
   (state <s> ^attribute state
              ^choices none
             -^operator.name wait)
-->
   (<s> ^operator <o> + =)
   (<o> ^name wait)}
