## Move Operator Proposal
# If the task is tanksoar and the tank is not blocked in the forward
# direction, propose the move operator.
sp {tanksoar*propose*move
   (state <s> ^name tanksoar
              ^io.input-link.blocked.forward no)
-->
   (<s> ^operator <o> +)
   (<o> ^name move
        ^actions.move.direction forward)}

## Turn Operator Proposal
# If the tank is blocked in the forward direction, and not blocked in the
# right or left directions, then propose the turn operator for the unblocked
# direction. Also create an indifferent preference for the operator.
# This operator will also turn on the radar and set the radar-power to 13.
# If the tank is blocked in the forward direction, and in both the right or
# left directions, then propose the turn operator left.
sp {propose*turn
   (state <s> ^name tanksoar
              ^io.input-link.blocked <b>)
   (<b> ^forward yes
        ^ { << left right >> <direction> } no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions <a>)
   (<a> ^rotate.direction <direction>
        ^radar.switch on
        ^radar-power.setting 13)}

sp {propose*turn*backward
   (state <s> ^name tanksoar
              ^io.input-link.blocked <b>)
   (<b> ^forward yes ^left yes ^right yes)
-->
   (<s> ^operator <o> +)
   (<o> ^name turn
        ^actions.rotate.direction left)}

## Radar-off Operator Proposal
# If the radar is on but no energy, health, missiles and tanks visible,
# then propose the radar-off operator
## Radar-off Search Control
# If radar-off is proposed, then prefer it to move and turn.
sp {propose*radar-off
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> ^radar-status on
        -^radar.<< energy health missiles tank >>)
-->
   (<s> ^operator <o> +)
   (<o> ^name radar-off
        ^actions.radar.switch off)}
sp {select*radar-off*move
   (state <s> ^name tanksoar
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name radar-off)
   (<o2> ^name << turn move >>)
-->
   (<s> ^operator <o1> > <o2>)}


